// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Zero x
    Mux16(a=true, b=false, sel=zx, out=notZx);
    And16(a=notZx, b=x, out=zerox);
    // negate X
    Not16(in=zerox, out=notx);
    Mux16(a=zerox, b=notx, sel=nx, out=ZNx); 

    // Zero y
    Mux16(a=true, b=false, sel=zy, out=notZy);
    And16(a=notZy, b=y, out=zeroy);
    // negate y
    Not16(in=zeroy, out=noty);
    Mux16(a=zeroy, b=noty, sel=ny, out=ZNy);

    // implement f signal
    And16(a=ZNx, b=ZNy, out=andFxy);
    Add16(a=ZNx, b=ZNy, out=addFxy);
    Mux16(a=andFxy, b=addFxy, sel=f, out=fout);

    // negate output
    Not16(in=fout, out=notout);
    Mux16(a=fout, b=notout, sel=no, out[0..7]=outleft, out[8..15]=outright, out[15]=outng, out=out);
    
    // zr
    Or8Way(in=outleft, out=or1);
    Or8Way(in=outright, out=or2);
    Or(a=or1, b=or2, out=notzr);
    Not(in=notzr, out=zr);

    // ng
    And(a=outng, b=true, out=ng);
}